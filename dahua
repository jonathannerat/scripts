#!/bin/perl

use v5.14;
use File::Basename;
use Getopt::Long qw(:config pass_through);
use IPC::Run3 'run3';
use constant { HD => 0, SD => 1 };

use lib dirname(__FILE__) . '/perl';
use Utils;

my $config = $ENV{XDG_CONFIG_HOME} . '/dahua.conf';
my $res    = SD;
my $all    = 0;

GetOptions(
    'config=s' => \$config,
    'hd'       => sub { $res = HD },
    'sd'       => sub { $res = SD },
    'all'      => \$all
);

# Parse config {{{
$config = Utils::parse_config $config,
  {
    host => '127.0.0.1',
    port => '554',
    user => 'admin'
  };
my $pass = $config->{pass};

my @channels;
while ( my ( $key, $value ) = each %$config ) {
    if ( $key eq 'passCmd' ) {
        $pass = qx($value);
        chomp $pass;
    } elsif ( $key =~ /channel\[(\d+)\]/ ) {
        chomp $value;
        push @channels, { ID => $1, NAME => $value };
    }
}

$config->{pass} = $pass;

# }}}

my @choices;
if ($all) {    # open all channels
    @choices = ( 0 .. @channels - 1 );
} else {    # pick channels with dmenu
    my $search;

    if (@ARGV) {
        $search = $ARGV[0];
        @choices =
          grep { $channels[$_]->{NAME} =~ /$search/i } ( 0 .. @channels - 1 );
    }

    if ( @choices == 0 ) {
        my @yad_channels = map {($_->{ID}, $_->{NAME})} sort {$a->{ID} cmp $b->{ID}} @channels;
        my $output;
        my $cmd   = [
            'yad', '--list', '--multiple',
            '--title=DAHUA', '--text=Select the cameras to show', '--image=camera-web',
            '--width=300', '--height=200',
            '--column=ID', '--column=Channel',
            @yad_channels
        ];

        run3 $cmd, undef, \$output;

        # filter output IDs only
        @choices = map {substr $_, 0, index($_, '|')} split "\n", $output;
    }
}

for my $channel ( map { $channels[$_] } @choices ) {
    my ( $user, $pass, $host, $port, $chid, $chname, $res ) = (
        $config->{user}, $config->{pass},  $config->{host}, $config->{port},
        $channel->{ID},  $channel->{NAME}, $res,
    );
    my $rtsp = "rtsp://$user:$pass\@$host:$port/cam/realmonitor?channel=$chid&subtype=$res";

    system "mpv --really-quiet --aid=no --title='Camera: $channel->{NAME}' '$rtsp' &";
}

# vi: fdm=marker
